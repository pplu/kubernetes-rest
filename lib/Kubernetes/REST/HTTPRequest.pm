package Kubernetes::REST::HTTPRequest;
  use Moo;
  use Types::Standard qw/Str HashRef/;

  has server => (is => 'ro');
  has credentials => (is => 'ro');

  sub authenticate {
    my $self = shift;
    my $auth = $self->credentials;
    if (defined $auth) {
      $self->headers->{ Authorization } = 'Bearer ' . $auth->token;
    }
  }

  has uri => (is => 'rw', isa => Str);
  has method => (is => 'rw', isa => Str);
  has url => (is => 'rw', isa => Str, lazy => 1, default => sub {
    my $self = shift;
    return $self->server->endpoint . $self->uri if $self->server;
    return '';
  });
  has headers => (is => 'rw', isa => HashRef, default => sub { {} });
  has parameters => (is => 'rw', isa => HashRef);
  has content => (is => 'rw', isa => Str);

1;

=encoding UTF-8

=head1 NAME

Kubernetes::REST::HTTPRequest - HTTP request object

=head1 SYNOPSIS

    use Kubernetes::REST::HTTPRequest;

    my $req = Kubernetes::REST::HTTPRequest->new(
        method => 'GET',
        url => 'https://kubernetes.local:6443/api/v1/pods',
        headers => { 'Authorization' => 'Bearer token' },
    );

=head1 DESCRIPTION

Internal HTTP request object used by L<Kubernetes::REST>.

=attr server

Optional. L<Kubernetes::REST::Server> instance for building the full URL.

=attr credentials

Optional. L<Kubernetes::REST::AuthToken> instance for authentication.

=attr uri

The URI path (e.g., C</api/v1/pods>).

=attr method

The HTTP method (GET, POST, PUT, DELETE, etc.).

=attr url

The complete URL. If not provided, constructed from C<server> and C<uri>.

=attr headers

Hashref of HTTP headers.

=attr parameters

Hashref of query parameters.

=attr content

The request body content (typically JSON).

=method authenticate()

Add authentication header from the C<credentials> attribute.

=cut
